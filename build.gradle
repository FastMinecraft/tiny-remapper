plugins {
	id 'java-library'
	id 'maven-publish'
//	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.12.1'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'dev.fastmc.maven-repo' version '1.0.0'
}

//version = '1.8.' + (System.getenv("GITHUB_RUN_NUMBER") ?: "9999")
version = '1.9-SNAPSHOT'

def ENV = System.getenv()

group = 'dev.architectury'
archivesBaseName = 'tiny-remapper'

repositories {
	mavenCentral()
}

dependencies {
	api 'org.ow2.asm:asm:9.4'
	api 'org.ow2.asm:asm-commons:9.4'
	implementation 'org.ow2.asm:asm-tree:9.4'
	implementation 'org.ow2.asm:asm-util:9.4'
	implementation 'it.unimi.dsi:fastutil:7.1.0'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

shadowJar {
	relocate 'net.fabricmc.tinyremapper', 'dev.architectury.tinyremapper'
	archiveClassifier = null
	configurations = [project.configurations.runtimeClasspath]
}

test {
	useJUnitPlatform()
	testLogging {
		showStandardStreams = true
		exceptionFormat = 'full'
		events = ['passed', 'failed', 'skipped']
	}
}

jar {
	manifest {
		attributes	'Implementation-Title': 'TinyRemapper',
					'Implementation-Version': archiveVersion,
					'Main-Class': "dev.architectury.tinyremapper.Main"
	}
	archiveClassifier = 'raw'
}

//checkstyle {
//	configFile = file("checkstyle.xml")
//	toolVersion = '8.31'
//}

spotless {
	ratchetFrom 'origin/master'
	enforceCheck false

	java {
		licenseHeaderFile(rootProject.file('HEADER')).yearSeparator(', ')
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact shadowJar
		}
	}

	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
	doFirst {
		def xml = new URL("https://maven.fabricmc.net/net/fabricmc/tiny-remapper/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)
		def versions = metadata.versioning.versions.version*.text();
		if (versions.contains(version)) {
			throw new RuntimeException("${version} has already been released!")
		}
	}
}

publish.mustRunAfter checkVersion
